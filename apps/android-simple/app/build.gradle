import java.nio.file.Paths

plugins {
    id 'com.android.application'
}

// Change the default value to "armeabi-v7a" if you need
def targetAbi = System.getenv("ABI") ?: "arm64-v8a"

def targetArch =  targetAbi == "arm64-v8a" ? "aarch64-linux-android":"arm-linux-android"
println "Target ABIs: $targetAbi"
println "Target Arch: $targetArch"

def onemlBootcampRootPath = "../../../"
def onemlJavaBindingsPath = Paths.get(
        onemlBootcampRootPath, String.format("assets/binaries/%s/bindings/java", targetArch)).toString()

println "Java bindings path: $onemlJavaBindingsPath"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    // NDK version should be the same as one in model config
    ndkVersion "21.0.6113669"

    defaultConfig {
        applicationId "com.sertiscorp.oneml.onemlsimpleapp"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 5
        versionName "0.5.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags "-fopenmp -static-openmp"
                abiFilters targetAbi
                arguments '-DANDROID_STL=c++_shared'
            }
        }
        ndk {
            abiFilters targetAbi
        }
    }

    externalNativeBuild {
        cmake {
            path  Paths.get(onemlJavaBindingsPath, "face/CMakeLists.txt")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest {
            assets.srcDirs = [Paths.get(onemlBootcampRootPath, "assets/images")]
        }
    }
}

dependencies {
    implementation fileTree(dir: Paths.get(onemlJavaBindingsPath, "face/oneml"), include: ['*.jar'])

    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.erikagtierrez.multiple_media_picker:multiple-media-picker:1.0.5'

    implementation 'androidx.appcompat:appcompat:1.3.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
