import java.nio.file.Paths

plugins {
    id 'com.android.application'
}

// Change the default value to "armeabi-v7a" if you need
def targetAbi = System.getenv("ABI") ?: "arm64-v8a"

def targetArch =  targetAbi == "arm64-v8a" ? "aarch64-linux-android":"arm-linux-android"
println "Target ABIs: $targetAbi"
println "Target Arch: $targetArch"

def onemlBootcampRootPath = "../../../"
def onemlJavaBindingsPath = Paths.get(
        onemlBootcampRootPath, String.format("assets/binaries/%s/bindings/java", targetArch)).toString()

println "Java bindings path: $onemlJavaBindingsPath"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    // NDK version should be the same as one in model config
    ndkVersion "21.0.6113669"

    defaultConfig {
        applicationId "org.sertiscorp.oneml.onemlcamera"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 9
        versionName "v0.9.0"

        ndk {
            abiFilters targetAbi
        }
        externalNativeBuild {
            cmake {
                cppFlags "-fopenmp -static-openmp"
                abiFilters targetAbi
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    externalNativeBuild {
        cmake {
            path  Paths.get(onemlJavaBindingsPath, "face/CMakeLists.txt")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: Paths.get(onemlJavaBindingsPath, "face/oneml"), include: ["*.jar"])

    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    implementation "com.google.android.material:material:1.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "com.android.support.test:rules:1.0.2"
    androidTestImplementation "com.google.truth:truth:1.0.1"
}
